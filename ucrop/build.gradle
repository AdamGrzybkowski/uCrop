plugins {
    id 'com.android.library'
    id "com.automattic.android.publish-to-s3"
}

repositories {
    google()
    mavenCentral()
}

android {
    compileSdk 33
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 33

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }

    resourcePrefix 'ucrop_'

}

dependencies {
    implementation "androidx.appcompat:appcompat:${androidx_appcompat_version}"
    implementation "androidx.exifinterface:exifinterface:${androidx_exifinterface_version}"
    implementation "androidx.transition:transition:${androidx_transition_version}"
    // OkHttp3 versions above 3.12.x don't support pre-Lollipop Android versions (API 21)
    implementation "com.squareup.okhttp3:okhttp:3.12.13"
}

project.afterEvaluate {
    publishing {
        publications {
            uCropPublication(MavenPublication) {
                from components.release

                groupId "com.automattic"
                artifactId "ucrop"
                // version is set by 'publish-to-s3' plugin
            }
        }
    }
}
